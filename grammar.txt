program → (function | statement)*

function → 'def' IDENTIFIER '(' parameters ')' ':' block
parameters → (parameter (',' parameter)*)?
parameter → IDENTIFIER ('=' expression)?

block → NEWLINE INDENT statement+ DEDENT

statement → assignment_stmt
          | return_stmt
          | iterative_stmt
          | conditional_stmt
          | function_call_stmt
          | pass_stmt
          | break_stmt
          | continue_stmt
          | import_stmt
          | raise_stmt
          | try_stmt
          | class_def

assignment_stmt → identifier_list ASSIGN_OP expression_list
identifier_list → (IDENTIFIER | dotted_name) (',' (IDENTIFIER | dotted_name))*
expression_list → expression (',' expression)*

ASSIGN_OP → '=' | '+=' | '-=' | '*=' | '/=' | '%=' | '//=' | '**='

return_stmt → 'return' expression?
pass_stmt → 'pass'
break_stmt → 'break'
continue_stmt → 'continue'

import_stmt → 'import' dotted_name ('as' IDENTIFIER)? (',' dotted_name ('as' IDENTIFIER)?)* 
            | 'from' dotted_name 'import' (IDENTIFIER ('as' IDENTIFIER)? | '*')

raise_stmt → 'raise' expression

try_stmt → 'try' ':' block
         ('except' (IDENTIFIER ('as' IDENTIFIER)? )? ':' block)+
         ('else' ':' block)?
         ('finally' ':' block)?

class_def → 'class' IDENTIFIER ('(' IDENTIFIER ')')? ':' class_block

class_block → NEWLINE INDENT class_member+ DEDENT
class_member → function | assignment_stmt

iterative_stmt → 'while' (not)? expression ':' block
               | 'for' IDENTIFIER (not)? 'in' expression ':' block

conditional_stmt → 'if' (not)? expression ':' block ('elif' (not)? expression ':' block)* ('else' ':' block)?

function_call_stmt → dotted_name '(' arguments ')'

arguments → (expression (',' expression)*)?

expression → or_expr

or_expr → and_expr ('or' and_expr)*
and_expr → not_expr ('and' not_expr)*
not_expr → 'not' not_expr | comparison
comparison → arithmetic_expr (OP arithmetic_expr)*

OP → '==' | '!=' | '<' | '>' | '>=' | '<='

arithmetic_expr → term (('+' | '-') term)*
term → factor (('*' | '/' | '%') factor)*

factor → INTEGER
       | FLOAT
       | STRING_LITERAL
       | True
       | False
       | IDENTIFIER
       | dotted_name
       | list_literal
       | tuple_literal
       | dict_literal
       | set_literal
       | '(' expression ')'
       | dotted_name '(' arguments ')'  // method call
       | IDENTIFIER '(' arguments ')'   // function call
       | unary_op factor

dotted_name → IDENTIFIER ('.' IDENTIFIER)*
unary_op → '+' | '-' | '~' | 'not'

list_literal → '[' (expression (',' expression)*)? ']'
tuple_literal → '(' (expression (',' expression)+) ')'
dict_literal → '{' (expression ':' expression (',' expression ':' expression)*)? '}'
set_literal → '{' expression (',' expression)* '}'
